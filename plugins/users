#!perl

HELP "<account>" => "[Pay with your account and] show balance";
HELP "list" => "List accounts and balances";
HELP "shame" => "Display Hall of Shame (negative balances)";
HELP "printbarcodes" => "Sends a scannable barcode list of users to the printer";

sub command :Tab(list,ls,shame,USERS) {
    my ($self, $cart, $command) = @_;

    return $self->list  if $command eq 'list';
    return $self->list  if $command eq 'ls';
    return $self->shame if $command eq 'shame';
    return $self->printbarcodes  if $command eq 'printbarcodes';

    my $user = parse_user($command)
        or return NEXT;

    return $self->balance($user) if not $cart->size;

    $cart->checkout($user);

    return ACCEPT;
}

sub hook_checkout {
    my ($class, $cart, $user, $transaction_id) = @_;

    my $line = "-" x 40;

    say "/$line";
    say "| Final (transaction ID = $transaction_id):";
    $cart->display("| ");
    say "\\$line";
}

sub list {
    system "sort -f revbank.accounts | grep -v ^# | perl -pe's/( -[\\d.]+)/\\e[31;1m\$1\\e[0m/' | more";
    return ACCEPT;
}

sub shame {
    system "sort -k2 -n revbank.accounts | grep -v ^# | grep -- ' -' | perl -pe's/( -[\\d.]+)/\\e[31;1m\$1\\e[0m/' | more";
    return ACCEPT;
}

sub balance {
    my ($self, $u) = @_;
    printf "Balance for $u is \e[1m%+.2f\e[0m\n", RevBank::Users::balance($u);
    say "NB: Products/amounts/commands FIRST, username LAST.";
    return ABORT;
}

sub printbarcodes {
   say "Making contact with indicio";
   system "~/revbank/printbarcodelist.sh";
   say "Printing list, check the printer";
   return ACCEPT;
}
  
